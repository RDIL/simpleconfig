plugins {
    id "java"
    id "maven"
    id "maven-publish"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.google.code.gson:gson:2.8.6"
}

version "2.0.0"
group "rocks.rdil"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

jar {
    from "LICENSE"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    description "Creates a source jar."
    classifier "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    description "Creates a javadoc jar."
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

uploadArchives {
    repositories.mavenDeployer {
        if (project.hasProperty("deployPassword")) {
            repository(url: "https://rdil.mycloudrepo.io/repositories/java-libs") {
                authentication(userName: "deploy@rdil.rocks", password: project.properties.deployPassword)
            }
        }

        pom {
            groupId = project.group
            version = project.version
            artifactId = project.archivesBaseName
            project {
                name project.archivesBaseName
                packaging "jar"
                description "A simple configuration system based on JSON"
                url "https://github.com/RDIL/simpleconfig"

                scm {
                    url "https://github.com/RDIL/simpleconfig"
                    connection "scm:git:git://github.com/RDIL/simpleconfig.git"
                    developerConnection "scm:git:git@github.com:RDIL/simpleconfig.git"
                }

                issueManagement {
                    system "github"
                    url "https://github.com/RDIL/simpleconfig/issues"
                }

                licenses {
                    license {
                        name "The MIT License"
                        url "https://opensource.org/licenses/MIT"
                        distribution "repo"
                    }
                }
            }
        }
    }
}
